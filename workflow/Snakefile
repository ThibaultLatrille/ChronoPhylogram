import os

FOLDER = os.path.abspath('.')

BURN_IN, UNTIL = 1000, 2000
datasets = ["Mammal", "Primate"]
sexes = ["m", "f"]
logTs = [True]

bayescode_path = {}
for executable in ["nodetraits", "readnodetraits"]:
    exec_path = os.path.join(FOLDER,f'utils/BayesCode/bin/{executable}')
    if not os.path.exists(exec_path):
        # Find executable in the path using whereis. If not found, raise an error.
        split = os.popen(f'whereis {executable}').read().split()
        if len(split) > 1:
            exec_path = split[1].strip()
        else:
            raise FileNotFoundError(f'{executable} not found. Please install BayesCode and add it to your path.')
    bayescode_path[executable] = exec_path
    print(f"Found {executable} at {exec_path}")


def data_dS(dataset):
    dico_dS = {"PrimateDiv": "science.abn7829.primates/science.abn7829_data_s3.nw.tree"}
    dico_dS["PrimateDivNeu"] = dico_dS["PrimateDiv"].replace("nw.tree","scaled.nw.tree")
    timescale = "science.abl8189.Timescale"
    dico_dS["MammalC30"] = f"{timescale}/PreliminaryTopologies/X_lessGC40_241.tree"
    dico_dS["MammalC75"] = f"{timescale}/PreliminaryTopologies/200Mammals_X_lessGC40_75consensus.tr"
    dico_dS["MammalDiv"] = f"{timescale}/Table_S2_RootedTrees/Concatenation_HRA_neutral_241_10miss_rooted.tree"
    dico_dS["MammalTime"] = dico_dS["PrimateTime"] = "mammals_timetree.nwk"
    return dico_dS[dataset]


rule all:
    input: expand(f"{FOLDER}/results/{{dataset}}.pdf", dataset=datasets)


rule scale_trees:
    input:
        script=f"{FOLDER}/scripts/scale_tree.py",
        tree_1=lambda wildcards: f"{FOLDER}/data/{data_dS(wildcards.dataset + 'Div')}",
        tree_2=lambda wildcards: f"{FOLDER}/data/{data_dS(wildcards.dataset + 'Time')}"
    output:
        tree_1=f"{FOLDER}/data_processed/{{dataset}}.scaled.Div.tree",
        tree_2=f"{FOLDER}/data_processed/{{dataset}}.scaled.Time.tree"
    shell:
        'python3 {input.script} --tree_1 {input.tree_1} --tree_2 {input.tree_2} --tree_output_1 {output.tree_1} --tree_output_2 {output.tree_2}'

rule pre_processed_traits:
    input:
        script=f"{FOLDER}/scripts/pre_processed_traits_mammals.py",
        input_tree=f"{FOLDER}/data_processed/{{dataset}}.scaled.{{gram}}.tree",
        input_traits=f"{FOLDER}/data/Tsuboi_etal_NEE_mammal.csv"
    output:
        tree=f"{FOLDER}/data_processed/mammals/{{dataset}}_{{gram}}_{{sex}}_{{logT}}.tree",
        traits=f"{FOLDER}/data_processed/mammals/{{dataset}}_{{gram}}_{{sex}}_{{logT}}.traits.tsv"
    params:
        phenotype=lambda wildcards: f"--log_transform {wildcards.logT} --sex {wildcards.sex}"
    log:
        stdout=f"{FOLDER}/data_processed/mammals/{{dataset}}_{{gram}}_{{sex}}_{{logT}}.preprocessed.log"
    shell:
        'python3 {input.script} {params.phenotype} --input_tree {input.input_tree}  --input_traits {input.input_traits}'
        ' --output_tree {output.tree} --output_traits {output.traits} > {log.stdout}'

rule run_inference:
    input:
        exec=bayescode_path["nodetraits"],
        traits=rules.pre_processed_traits.output.traits,
        tree=rules.pre_processed_traits.output.tree
    output:
        run=f"{FOLDER}/data_processed/inference/{{dataset}}_{{gram}}_{{sex}}_{{logT}}.run"
    params:
        chain=f"{FOLDER}/data_processed/inference/{{dataset}}_{{gram}}_{{sex}}_{{logT}}",
        until=f"--until {UNTIL}"
    log:
        stdout=f"{FOLDER}/data_processed/inference/{{dataset}}_{{gram}}_{{sex}}_{{logT}}.log"
    shell:
        '{input.exec} {params.until} --uniq_kappa --df 1 --tree {input.tree} --traitsfile {input.traits} {params.chain}'
        ' > {log.stdout}'

rule merge_trace:
    input:
        script=f"{FOLDER}/scripts/merge_results_mammals.py",
        bayescode=expand(f"{FOLDER}/data_processed/inference/{{{{dataset}}}}_{{gram}}_{{sex}}_{{logT}}.run",
            gram=["Div", "Time"],sex=sexes,logT=logTs)
    output:
        tsv=f"{FOLDER}/results/{{dataset}}.pdf"
    shell:
        'python3 {input.script} --bayescode {input.bayescode} --output {output.tsv}'
