import os

FOLDER = os.path.abspath('.')

BURN_IN, UNTIL = 1000, 2000
datasets = {"Mammal": {"traits": ["bodyMass", "brainMass"], "sexes": ["m", "f", "b"], "rb": ["simple_BM_Switchnodes", "simple_BM_SwitchREML"]},
#           "Lizard": {"traits": ["bodyMass"], "sexes": ["b"], "rb": ["simple_BM_SwitchREML"]},
#           "Primate": {"traits": ["bodyMass", "brainMass"], "sexes": ["m", "f", "b"], "rb": ["simple_BM_Switchnodes"]},
            "Combine": {"traits": ["bodyMass", "brainMass"], "sexes": ["b"], "rb": ["simple_BM_Switchnodes", "simple_BM_SwitchREML"]}}

exec_path = "/opt/homebrew/Caskroom/miniforge/base/envs/osx-64/bin/rb"
if not os.path.exists(exec_path):
    # Find executable in the path using whereis. If not found, raise an error.
    split = os.popen(f'whereis rb').read().split()
    if len(split) > 1:
        exec_path = split[1].strip()
    else:
        raise FileNotFoundError(f'rb not found. Please install RevBayes and add it to your path.')
print(f"Found rb at {exec_path}")


def data_tree(dataset):
    dtree = {"Primate": "science.abn7829.primates/science.abn7829_data_s3.scaled.nw.tree"}
    r = "science.abl8189.Timescale/Table_S2_RootedTrees"
    dtree["Combine"] = dtree["Mammal"] = f"{r}/Concatenation_HRA_neutral_241_10miss_rooted.tree"
    dtree["Lizard"] = "Lizard/squam_shl_new.tre"
    return f"{FOLDER}/data/{dtree[dataset]}"


def data_traits(dataset):
    ttree = {"Combine": "COMBINE/trait_data_reported.csv", "Mammal": "Tsuboi_etal_NEE_mammal.csv",
             "Primate": "Tsuboi_etal_NEE_mammal.csv", "Lizard": "Lizard/Table_S2_JAN2024.csv"}
    return f"{FOLDER}/data/{ttree[dataset]}"


def script_preprocess(dataset):
    dscript = {"Combine": "pre_processed_traits_COMBINE.py", "Mammal": "pre_processed_traits_mammals.py",
               "Primate": "pre_processed_traits_mammals.py", "Lizard": "pre_processed_traits_lizard.py"}
    return f"{FOLDER}/scripts/{dscript[dataset]}"


rule all:
    input:
        f"{FOLDER}/results/empiricalRevBayes.tsv"

rule reconstructed_chronogram:
    input:
        script=f"{FOLDER}/scripts/calib_pl.R",
        tree=lambda wildcards: data_tree(wildcards.dataset),
    output:
        tree=f"{FOLDER}/data_empirical/{{dataset}}.reconstructed.Chrono.tree"
    shell:
        'Rscript {input.script} {input.tree} {output.tree}'

rule scale_trees:
    input:
        script=f"{FOLDER}/scripts/scale_tree.py",
        tree_1=lambda wildcards: data_tree(wildcards.dataset),
        tree_2=f"{FOLDER}/data_empirical/{{dataset}}.reconstructed.Chrono.tree"
    output:
        tree_1=f"{FOLDER}/data_empirical/{{dataset}}.scaled.Phylo.tree",
        tree_2=f"{FOLDER}/data_empirical/{{dataset}}.scaled.Chrono.tree"
    shell:
        'python3 {input.script} --tree_1 {input.tree_1} --tree_2 {input.tree_2} --tree_output_1 {output.tree_1} --tree_output_2 {output.tree_2}'

rule pre_processed_traits:
    input:
        script=lambda wildcards: script_preprocess(wildcards.dataset),
        input_tree=f"{FOLDER}/data_empirical/{{dataset}}.scaled.{{gram}}.tree",
        input_traits=lambda wildcards: data_traits(wildcards.dataset)
    output:
        tree=f"{FOLDER}/data_empirical/processed/{{dataset}}_{{gram}}_{{sex}}_{{trait}}.tree",
        traits=f"{FOLDER}/data_empirical/processed/{{dataset}}_{{gram}}_{{sex}}_{{trait}}.traits.tsv"
    params:
        phenotype=lambda wildcards: f"--trait {wildcards.trait} --sex {wildcards.sex}"
    log:
        stdout=f"{FOLDER}/data_empirical/processed/{{dataset}}_{{gram}}_{{sex}}_{{trait}}.preprocessed.log"
    shell:
        'python3 {input.script} {params.phenotype} --input_tree {input.input_tree}  --input_traits {input.input_traits}'
        ' --output_tree {output.tree} --output_traits {output.traits} > {log.stdout}'

rule convert_to_RevBayes:
    input:
        script=f"{FOLDER}/scripts/convert_to_nexus.py",
        tree=rules.pre_processed_traits.output.tree,
        traits=rules.pre_processed_traits.output.traits
    output:
        nexus_tree=f"{FOLDER}/data_empirical/processed/{{dataset}}_{{gram}}_{{sex}}_{{trait}}.tree.nex",
        nexus_traits=f"{FOLDER}/data_empirical/processed/{{dataset}}_{{gram}}_{{sex}}_{{trait}}.traits.nex"
    shell:
        'python3 {input.script} --input_tree {input.tree} --input_traits {input.traits} --output_tree {output.nexus_tree} --output_traits {output.nexus_traits}'

rule run_RevBayes:
    input:
        exec=exec_path,
        rev_file=f"{FOLDER}/scripts/mcmc_{{rb}}.Rev",
        timetree=f"{FOLDER}/data_empirical/processed/{{dataset}}_Chrono_{{sex}}_{{trait}}.tree.nex",
        nuctree=f"{FOLDER}/data_empirical/processed/{{dataset}}_Phylo_{{sex}}_{{trait}}.tree.nex",
        traits=f"{FOLDER}/data_empirical/processed/{{dataset}}_Chrono_{{sex}}_{{trait}}.traits.nex"
    output:
        log=f"{FOLDER}/data_empirical/RevBayes_{{dataset}}/{{sex}}_{{trait}}/{{rb}}.log.gz"
    params:
        folder=lambda wildcards: f"{FOLDER}/data_empirical/RevBayes_{wildcards.dataset}/{wildcards.sex}_{wildcards.trait}",
        prefix=lambda wildcards: f"{FOLDER}/data_empirical/RevBayes_{wildcards.dataset}/{wildcards.sex}_{wildcards.trait}/{wildcards.rb}"
    shell:
        'mkdir -p {params.folder};'
        'cp {input.timetree} {params.folder}/tree_time.nex;'
        'cp {input.nuctree} {params.folder}/tree_nuc.nex;'
        'cp {input.traits} {params.folder}/traits.nex;'
        'cd {params.folder} && {input.exec} {input.rev_file};'
        'for f in {params.prefix}*.log; do if [ -f $f ]; then gzip -f $f; fi; done;'

rule gather_RevBayes_log:
    input:
        script=f"{FOLDER}/scripts/plot_empirical_RevBayes.py",
        rb_bm=lambda wildcards: expand(f"{FOLDER}/data_empirical/RevBayes_{wildcards.dataset}/{{sex}}_{{trait}}/{{rb}}.log.gz",
            rb=datasets[wildcards.dataset]["rb"],trait=datasets[wildcards.dataset]["traits"],
            sex=datasets[wildcards.dataset]["sexes"]),
    output:
        plot=f"{FOLDER}/results/empiricalRevBayes_{{dataset}}.pdf",
        tsv=f"{FOLDER}/results/empiricalRevBayes_{{dataset}}.tsv"
    params:
        folder=f"{FOLDER}/data_empirical/RevBayes_{{dataset}}"
    shell:
        'python3 {input.script} --folder {params.folder} --output_pdf {output.plot} --output_tsv {output.tsv}'

rule merge_tsv:
    input:
        script=f"{FOLDER}/scripts/merge_results_empirical.py",
        tsv=expand(f"{FOLDER}/results/empiricalRevBayes_{{dataset}}.tsv",dataset=datasets.keys())
    output:
        tsv=f"{FOLDER}/results/empiricalRevBayes.tsv"
    shell:
        'python3 {input.script} --output_tsv {output.tsv} --input_tsv {input.tsv}'